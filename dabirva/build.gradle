plugins {
    id "com.android.library"
    id "kotlin-android"
    id "kotlin-kapt"
    id "de.mannodermaus.android-junit5"
    id "jacoco"
    id "org.jetbrains.dokka"
    id "signing"
    id "maven-publish"
}

ext {
    GROUP_ID = "com.matbadev.dabirva"
    ARTIFACT_ID = "dabirva"
    VERSION_NAME = "1.0.0-SNAPSHOT"
    SNAPSHOT_BUILD = VERSION_NAME.endsWith("-SNAPSHOT")
}

jacoco {
    toolVersion = "0.8.7"
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30
        versionName VERSION_NAME
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            testCoverageEnabled true
        }
    }

    buildFeatures {
        dataBinding true
    }

    compileOptions {
        sourceCompatibility JAVA_VERSION
        targetCompatibility JAVA_VERSION
    }

    kotlinOptions {
        jvmTarget = JAVA_VERSION
    }

    packagingOptions {
        exclude "META-INF/LICENSE*"
    }

    // CI should run tests in release build type but when working in Android Studio debug should be selected
    testBuildType = project.hasProperty("ci") ? "release" : "debug"

    testOptions {
        // Save instrumented test results to same directory as unit test results
        resultsDir "$buildDir/test-results"
    }

    // See: https://developer.android.com/reference/tools/gradle-api/4.1/com/android/build/api/dsl/LintOptions
    lintOptions {
        abortOnError true
        warningsAsErrors false
        textReport false
        xmlReport true
        htmlReport false
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$KOTLIN_VERSION"

    api "androidx.recyclerview:recyclerview:1.1.0"

    // Unit tests
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.7.0"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.7.0"

    // Android tests
    androidTestImplementation "androidx.test.ext:junit:1.1.2"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.3.0"
    androidTestImplementation "org.mockito:mockito-android:3.7.7"
}

task mergeJacocoReports(type: JacocoReport) {
    group = "reporting"
    description = "Merge Jacoco coverage reports into a single report"

    reports {
        xml.enabled = true
        html.enabled = false
        csv.enabled = false
    }

    sourceDirectories.setFrom "$projectDir/src/main/java"
    classDirectories.setFrom "$buildDir/tmp/kotlin-classes/release"
    executionData.setFrom fileTree(dir: projectDir, includes: [
            // Unit tests coverage
            "*.exec",
            // Instrumented tests coverage
            "build/outputs/code_coverage/**/*.ec",
    ])
}

task androidSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set("sources")
}

task androidJavadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    from "$buildDir/dokka/javadoc"
    archiveClassifier.set("javadoc")
}

afterEvaluate {
    publishing {
        publications {
            maven(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                // Additional artifacts to be included in upload.
                artifact androidSourcesJar
                artifact androidJavadocJar

                // Basic details for the generated POM.
                groupId GROUP_ID
                artifactId ARTIFACT_ID
                version VERSION_NAME

                // Metadata for the generated POM.
                pom {
                    name = "Dabirva"
                    description = "Data Binding RecyclerView Adapter for Android"
                    url = "https://github.com/matbadev/dabirva"
                    packaging = "aar"

                    licenses {
                        license {
                            name = "The Apache License, Version 2.0"
                            url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        }
                    }

                    developers {
                        developer {
                            id = "matbadev"
                            name = "Matthias BÃ¤uerle"
                            url = "https://www.matbadev.com/"
                        }
                    }

                    scm {
                        url = "https://github.com/matbadev/dabirva"
                        connection = "scm:git:git://github.com/matbadev/dabirva.git"
                        developerConnection = "scm:git:ssh://github.com:matbadev/dabirva.git"
                    }
                }
            }
        }

        repositories {
            maven {
                name = "centralS01Snapshots"
                url = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
                credentials {
                    username = findProperty("publishing.central.s01.username")
                    password = findProperty("publishing.central.s01.password")
                }
            }

            maven {
                name = "centralS01Staging"
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username = findProperty("publishing.central.s01.username")
                    password = findProperty("publishing.central.s01.password")
                }
            }
        }
    }

    signing {
        required { !SNAPSHOT_BUILD }
        sign publishing.publications.maven
    }
}

tasks.withType(Sign) {
    onlyIf { !SNAPSHOT_BUILD }
}
